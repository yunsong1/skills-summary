go atlas
mesh space.mult=1

x.m loc=0.0   spac=0.5
x.m loc=4.0   spac=0.5
x.m loc=5.0   spac=0.2
x.m loc=6.0   spac=0.5
x.m loc=9.0   spac=0.5
x.m loc=12.0  spac=0.2
x.m loc=13.0  spac=1
x.m loc=20.0  spac=1

y.m loc=-0.1   spac=0.1
y.m loc=1.0    spac=0.1
y.m loc=1.4    spac=0.2
y.m loc=2.0    spac=0.5
y.m loc=5.0    spac=0.5
y.m loc=6.0    spac=0.2
y.m loc=7.0    spac=0.5
y.m loc=8.0    spac=4.0
y.m loc=65.0  spac=4.0
y.m loc=69.0  spac=1.0
y.m loc=70.0  spac=0.2
y.m loc=71.0  spac=1.0
y.m loc=72.0  spac=0.2
y.m loc=74.0  spac=1.0
y.m loc=75.0  spac=1.0
y.m loc=84.0  spac=1.0

region  num=1  x.min=0  x.max=20  y.min=0.0   y.max=85.0  material=silicon
region  num=2  x.min=0  x.max=20  y.min=-0.1  y.max=0     material=oxy

electr  name=emitter     x.min=0   x.max=7   y.min=0  y.max=0
electr  name=gate        x.min=10  x.max=20  top
electr  name=collector   x.min=0   x.max=20  bot

doping uniform  n.type conc=5e13   reg=1
doping gauss   p.type conc=2e17   peak=0.3   junc=6   x.min=0  x.max=10  ratio.lat=0.6
doping gauss   n.type conc=5e19   peak=0.05  junc=0.6 x.min=5  x.max=10  ratio.lat=0.6
doping gauss   n.type conc=3e17   peak=72.0  junc=70
doping gauss   p.type conc=1e19   peak=83.5  junc=74

save outf=igbt.str
tonyplot igbt.str

models cvt srh print
method newton trap maxtrap=5
solve init

solve vgate=0.1 vstep=1 vfinal=3 name=gate

solve vgate=4.0   outf=solve_tmp1
solve vgate=4.5   outf=solve_tmp2
solve vgate=5.0   outf=solve_tmp3
solve vgate=5.5   outf=solve_tmp4
solve vgate=6.0   outf=solve_tmp5

load inf=solve_tmp1
log outf=1.log 
solve vcollector=0.0 vstep=0.2 vfinal=30.0 name=collector
load inf=solve_tmp2
log outf=2.log
solve vcollector=0.0 vstep=0.2 vfinal=30.0 name=collector
load inf=solve_tmp3
log outf=3.log
solve vcollector=0.0 vstep=0.2 vfinal=30.0 name=collector
load inf=solve_tmp4
log outf=4.log
solve vcollector=0.0 vstep=0.2 vfinal=30.0 name=collector
load inf=solve_tmp5
log outf=5.log
solve vcollector=0.0 vstep=0.2 vfinal=30.0 name=collector

tonyplot -overlay  1.log 2.log 3.log 4.log 5.log
quit
